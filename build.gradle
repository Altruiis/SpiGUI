plugins {
    id 'java'
}

group 'com.samjakob'
version '1.2.2-SNAPSHOT'

repositories {
    mavenCentral()

    // Paper
    maven {
        url = uri("https://repo.papermc.io/repository/maven-public/")
    }
}

dependencies {
    testCompileOnly group: 'junit', name: 'junit', version: '4.13.2'

    // Paper API
    compileOnly group: 'io.papermc.paper', name: 'paper-api', version: '1.18.2-R0.1-SNAPSHOT'
    testCompileOnly(group: 'io.papermc.paper', name: 'paper-api', version: '1.18.2-R0.1-SNAPSHOT')
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

task testJar(type: Jar) {
    from sourceSets.main.output.classesDirs
    from sourceSets.test.output.classesDirs
    from('src/test/resources') {
        include '**'
    }

    archiveBaseName.set("SpiGUITest")
}

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.java.sourceDirectories
}

task classesJar(type: Jar) {
    from sourceSets.main.output.classesDirs
}

task documentation(type: Javadoc) {
    source = sourceSets.main.java.sourceDirectories
    classpath += configurations.compileClasspath

    options.memberLevel = JavadocMemberLevel.PRIVATE
}

task javadocJar(type: Jar, dependsOn: documentation) {
    archiveClassifier.set('javadoc')
    from documentation.destinationDir
}

artifacts {
    archives classesJar
    archives javadocJar
    archives sourcesJar
}